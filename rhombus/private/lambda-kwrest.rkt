#lang racket/base
(require (for-syntax racket/base
                     syntax/parse/pre))

(provide lambda/kwrest
         case-lambda/kwrest)

;; ---------------------------------------------------------

(define-syntax lambda/kwrest
  (lambda (stx)
    (syntax-parse stx
      ;; This case is unused, because Racket `define` has to recognize
      ;; an immediate, unexpanded `lambda` in order to optimize
      ;; keyword arguments.
      #;
      [(_ #:name name
          #:arity _
          (~optional (~seq #:rest rest))
          (arg ...)
          b ...)
       #'(let ([name (lambda (~? (arg ... . rest)
                                 (arg ...))
                       b ...)])
           name)]
      [(_ #:name name
          #:arity (mask required-kws allowed-kws)
          (~optional (~seq #:rest rest))
          #:kwrest kwrest
          ((~alt (~seq kw:keyword (~or* [kw-arg kw-default] kw-arg))
                 (~or* [arg default] arg))
           ...)
          b ...)
       ;; assumption: optional by-position arguments must appear after
       ;; mandatory by-position arguments
       (define args (syntax->list #'(arg ...)))
       (define defaults (syntax->list #'((~? default) ...)))
       (define non-rest-max (length args))
       (define non-rest-min (- non-rest-max (length defaults)))
       (define-values (kwhash-proc-claws proc-claws)
         (for/lists (kwhash-proc-claws proc-claws)
                    ([n (in-inclusive-range non-rest-min non-rest-max)])
           (cond
             [(eqv? n non-rest-max)
              (values #`[(~? (kwrest #,@args . rest)
                             (kwrest #,@args))
                         (let* ((~@ [kw-arg (hash-ref kwrest 'kw
                                                      ;; guarded by reduced arity
                                                      (~? (lambda () kw-default)))]
                                    [kwrest (hash-remove kwrest 'kw)])
                                ...)
                           b ...)]
                      #`[(~? (#,@args . rest)
                             (#,@args))
                         (~? (apply kwhash-proc '#hashalw() #,@args rest)
                             (kwhash-proc '#hashalw() #,@args))])]
             [else
              (define given-args
                (for/list ([arg (in-list args)]
                           [_ (in-range n)])
                  arg))
              (define unsupplied-args
                (list-tail defaults (- n non-rest-min)))
              (values #`[(kwrest #,@given-args)
                         (kwhash-proc kwrest #,@given-args #,@unsupplied-args)]
                      #`[(#,@given-args)
                         (kwhash-proc '#hashalw() #,@given-args #,@unsupplied-args)])])))
       #`(procedure-reduce-keyword-arity-mask
          (letrec ([kwhash-proc (case-lambda #,@kwhash-proc-claws)])
            (make-keyword-procedure
             (lambda (ks vs . ps)
               (apply kwhash-proc (keyword-lists->hash ks vs) ps))
             (let ([name (case-lambda #,@proc-claws)])
               name)))
          'mask
          'required-kws
          'allowed-kws)])))

(define-syntax case-lambda/kwrest
  (lambda (stx)
    (syntax-parse stx
      [(_ #:name name
          #:arity _
          [(~optional (~seq #:rest rest))
           (arg ...)
           b ...]
          ...)
       #'(let ([name (case-lambda
                       [(~? (arg ... . rest)
                            (arg ...))
                        b ...]
                       ...)])
           name)]
      ;; assumption: all clauses must accept `#:kwrest`
      ;; We don't do anything fancy here, because dispatch code is
      ;; generated by `fun` itself.
      [(_ #:name name
          #:arity (mask required-kws allowed-kws)
          [(~optional (~seq #:rest rest))
           #:kwrest kwrest
           (arg ...)
           b ...]
          ...)
       #'(procedure-reduce-keyword-arity-mask
          (let ([kwhash-proc (case-lambda
                               [(~? (kwrest arg ... . rest)
                                    (kwrest arg ...))
                                b ...]
                               ...)])
            (make-keyword-procedure
             (lambda (ks vs . ps)
               (apply kwhash-proc (keyword-lists->hash ks vs) ps))
             (let ([name (case-lambda
                           [(~? (arg ... . rest)
                                (arg ...))
                            (~? (apply kwhash-proc '#hashalw() arg ... rest)
                                (kwhash-proc '#hashalw() arg ...))]
                           ...)])
               name)))
          'mask
          'required-kws
          'allowed-kws)])))

;; ---------------------------------------------------------

;; keyword-lists->hash : (Listof Keyword) (Listof V) -> (Hashof Keyword V)
(define (keyword-lists->hash ks vs)
  (for/hashalw ([k (in-list ks)]
                [v (in-list vs)])
    (values k v)))
